allprojects {
    repositories {
        mavenCentral()
        maven { url = "https://repo.spring.io/snapshot" }
        maven { url = "https://repo.spring.io/milestone" }
    }
}

apply plugin: 'info.solidsoft.pitest.aggregator'

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'info.solidsoft.pitest'
    apply plugin: 'io.spring.dependency-management'

    compileJava.dependsOn validateStructure

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    dependencies {
        implementation 'io.projectreactor:reactor-core'
        implementation 'io.projectreactor.addons:reactor-extra'
        testImplementation 'io.projectreactor.tools:blockhound-junit-platform:1.0.11.RELEASE'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'io.projectreactor:reactor-test'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }

    test {
        useJUnitPlatform()
    }

    pitest {
        pitestVersion = '1.19.0'
        junit5PluginVersion = '1.2.1'
        targetClasses = ['co.com.emorae.*']
        outputFormats = ['XML', 'HTML']
        threads = 8
        verbose = false
        exportLineCoverage = true
        timestampedReports = false
        failWhenNoMutations = false
        useClasspathFile = true
        excludedClasses = ['co.com.emorae.model.*']
        jvmArgs = [
                // To avoid: BlockHoundTestExecutionListener could not be instantiated
                '-XX:+AllowRedefinitionToAddDeleteMethods'
        ]
    }

    tasks.withType(Test).configureEach {
        if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_13)) {
            jvmArgs += [
                    "-XX:+AllowRedefinitionToAddDeleteMethods"
            ]
        }
    }

    test.finalizedBy(project.tasks.jacocoTestReport)

    jacocoTestReport {
        dependsOn test, 'pitest'
        reports {
            xml.required = true
            xml.outputLocation = layout.buildDirectory.file("reports/jacoco.xml")
            csv.required = false
            html.outputLocation = layout.buildDirectory.dir("reports/jacocoHtml")
        }
    }

}

pitestReportAggregate {
    doLast {
        def reportDir = layout.buildDirectory.dir("reports/pitest").get().asFile
        def consolidatedReport = new File(reportDir, 'mutations.xml')
        consolidatedReport.withWriter { writer ->
            writer.write("<mutations>\n")
            subprojects.each { subproject ->
                def xmlReport = subproject.layout.buildDirectory.file("reports/pitest/mutations.xml").get().asFile
                if (xmlReport.exists()) {
                    def xmlContent = xmlReport.text
                    xmlContent = xmlContent.replaceAll("<\\?xml[^>]*>", "")
                    xmlContent = xmlContent.replaceAll("</?mutations( partial=\"true\")?>", "")
                    writer.write(xmlContent.trim() + "\n")
                }
            }
            writer.write("</mutations>")  // Cierra la etiqueta <mutations> al final
        }
    }
}

jacoco {
    toolVersion = "${jacocoVersion}"
    reportsDirectory.set(layout.buildDirectory.dir("reports"))
}

tasks.register('jacocoMergedReport', JacocoReport) {
    dependsOn = [test, subprojects.jacocoTestReport, pitestReportAggregate]
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true'
    ]
}

tasks.named('wrapper') {
    gradleVersion = '8.13'
    distributionSha256Sum = '20f1b1176237254a6fc204d8434196fa11a4cfb387567519c61556e8710aed78'
    validateDistributionUrl = true
}
